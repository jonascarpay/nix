{ pkgs, ... }:
let
  pyPkgs = pkgs.python3Packages;
  colorz = pyPkgs.buildPythonPackage rec {
    pname = "colorz";
    version = "1.0.3";
    propagatedBuildInputs = with pyPkgs; [ numpy pillow scipy ];
    src = pyPkgs.fetchPypi {
      inherit pname version;
      sha256 = "0ghd90lgplf051fs5n5bb42zffd3fqpgzkbv6bhjw7r8jqwgcky0";
    };
  };
  haishoku = pyPkgs.buildPythonPackage rec {
    pname = "haishoku";
    version = "1.1.8";
    propagatedBuildInputs = with pyPkgs; [ pillow ];
    src = pyPkgs.fetchPypi {
      inherit pname version;
      sha256 = "1m6bzxlm4pfdpjr827k1zy9ym3qn411lpw5wiaqq2q2q0d6a3fg4";
    };
  };
  pywalfox = pyPkgs.buildPythonPackage rec {
    pname = "pywalfox";
    version = "2.6";
    # propagatedBuildInputs = with pyPkgs; [ pillow ];
    src = pyPkgs.fetchPypi {
      inherit pname version;
      sha256 = "0aci5npfzhgwz8smmbnmqbj524bj9ysgx5ny89q9l28cpisngc4n";
    };
  };

  epatch = pkgs.writeText "emacspatch" ''
    diff --git a/pywal/templates/colors.Xresources b/pywal/templates/colors.Xresources
    index 018c1e6..2b5f492 100644
    --- a/pywal/templates/colors.Xresources
    +++ b/pywal/templates/colors.Xresources
    @@ -1,11 +1,5 @@
     ! X colors.
     ! Generated by 'wal'
    -*foreground:        {foreground}
    -*background:        {background}
    -*.foreground:       {foreground}
    -*.background:       {background}
    -emacs*foreground:   {foreground}
    -emacs*background:   {background}
     URxvt*foreground:   {foreground}
     XTerm*foreground:   {foreground}
     UXTerm*foreground:  {foreground}
  '';

  mywal = pyPkgs.pywal.overrideAttrs (old: {
    propagatedBuildInputs = old.propagatedBuildInputs ++ [ colorz haishoku ];
    patches = old.patches ++ [ epatch ];
  });
in
{
  home.packages = [ mywal pywalfox ];
  xsession.initExtra = ''
    wal -i ~/Wallpapers/papes --backend haishoku
  '';
  programs.autorandr.hooks.postswitch.restart-wal = ''
    wal -i "$(cat /home/jmc/.cache/wal/wal)" -R
  '';
  programs.fish.shellInit = ''
    source ~/.cache/wal/colors.fish
    set -g theme_color_scheme terminal

    cat ~/.cache/wal/sequences &
  '';
  home.file.".config/wal/templates/colors.fish".text = ''
    # set fish_color_normal normal
    set fish_color_normal {foreground.strip}
    # set fish_color_command 005fd7
    set fish_color_command {color2.strip}
    # set fish_color_param 00afff
    set fish_color_param {color1.strip}
    # set fish_color_redirection 00afff
    set fish_color_redirection $fish_color_param
    # set fish_color_comment 990000
    set fish_color_comment {color8.strip}
    set fish_color_error ff0000
    # set fish_color_escape 00a6b2
    set fish_color_escape {color5.strip}
    # set fish_color_operator 00a6b2
    set fish_color_operator $fish_color_escape
    set fish_color_end {color4.strip}
    set fish_color_quote {color6.strip}
    set fish_color_autosuggestion 555 brblack
    set fish_color_user brgreen
    # set fish_color_host normal
    set fish_color_host $fish_color_normal
    set fish_color_valid_path --underline
    set fish_color_cwd green
    set fish_color_cwd_root red
    set fish_color_match --background=brblue
    set fish_color_search_match bryellow --background=brblack
    set fish_color_selection white --bold --background=brblack
    set fish_color_cancel -r
    set fish_pager_color_prefix white --bold --underline
    set fish_pager_color_completion
    # set fish_pager_color_description B3A06D yellow
    set fish_pager_color_description $fish_color_quote yellow
    set fish_pager_color_progress brwhite --background=cyan
    set fish_color_history_current --bold
  '';
}
